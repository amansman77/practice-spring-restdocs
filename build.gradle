plugins {
	id 'org.springframework.boot' version '2.1.7.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	// 1. Apply the Asciidoctor plugin.
	id "org.asciidoctor.convert" version "1.5.3"
}

group = 'com.ho.practice.restdocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	// 2. Add a dependency on spring-restdocs-asciidoctor in the asciidoctor configuration.
	// This will automatically configure the snippets attribute for use in your .adoc files to 
	// point to build/generated-snippets.
	// It will also allow you to use the operation block macro.
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.3.RELEASE'
	// 3. Add a dependency on spring-restdocs-mockmvc in the testCompile configuration.
	// If you want to use REST Assured rather than MockMvc, add a dependency on spring-restdocs-restassured instead.
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.3.RELEASE'
}

// 4. Configure a property to define the output location for generated snippets.
ext { 
	snippetsDir = file('build/generated-snippets')
}

// 5. Configure the test task to add the snippets directory as an output.
test { 
	outputs.dir snippetsDir
}

// 6. Configure the asciidoctor task
asciidoctor { 
	// 7. Configure the snippets directory as an input.
	inputs.dir snippetsDir
	// 8. Make the task depend on the test task so that the tests are run before the documentation is created.
	dependsOn test 
}

bootJar {
	// 9. Ensure that the documentation has been generated before the jar is built.
	dependsOn asciidoctor 
	// 10. Copy the generated documentation into the jar’s static/docs directory.
	from ("${asciidoctor.outputDir}/html5") { 
		into 'static/docs'
	}
}

// 인코딩 지정
tasks.withType(Test) {
	systemProperty "file.encoding", "UTF-8"
}
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'